apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 0
def versionBuild = 1 // bump for dogfood builds, public betas, etc.

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {

        minSdkVersion 23
        targetSdkVersion 27
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    ext{
        APP_NAME_DEBUG = "@string/app_name_debug"
        APP_NAME_RELEASE = "@string/app_name_release"
    }

    buildTypes {

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false

            // replace applicationLabel in manifest with appropriate app_name
            manifestPlaceholders = [ applicationLabel_1 : APP_NAME_RELEASE]
        }

        debug {
            minifyEnabled false
            // for giving a different application id to debug apk
            // to keep debug and release apk at the same time in test device
            applicationIdSuffix ".debug"
            versionNameSuffix '-DEBUG'

            // replace applicationLabel in manifest with appropriate app_name
            manifestPlaceholders = [ applicationLabel_1: APP_NAME_DEBUG]
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // android support libraries
    implementation "com.android.support:appcompat-v7:$supportLibraryVer"
    implementation "com.android.support:design:$supportLibraryVer"
    implementation "com.android.support:palette-v7:$supportLibraryVer"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVer"
    implementation "com.android.support:cardview-v7:$supportLibraryVer"

    // constraint layout
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVer"

    // glide - images
    implementation "com.github.bumptech.glide:glide:$glideVer"
    kapt "com.github.bumptech.glide:compiler:$glideVer"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glideVer"
    implementation "com.github.bumptech.glide:recyclerview-integration:$glideVer"

    // gson - json helper
    implementation "com.google.code.gson:gson:$gsonVer"

    // butterknife - view injection
    implementation "com.jakewharton:butterknife:$butterknifeVer"
    kapt "com.jakewharton:butterknife-compiler:$butterknifeVer"

    // retrofit - api calls
    implementation "com.squareup.retrofit2:retrofit:$retrofitVer"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVer"

    // okhttp - api calls
    implementation "com.squareup.okhttp3:okhttp:$okhttpVer"

    // dagger - dependency injection
    implementation "com.google.dagger:dagger:$daggerVer"
    kapt  "com.google.dagger:dagger-compiler:$daggerVer"
    implementation "com.google.dagger:dagger-android:$daggerVer"
    kapt  "com.google.dagger:dagger-android-processor:$daggerVer"
    implementation "com.google.dagger:dagger-android-support:$daggerVer"
    // if you upgrade this to latest version -> DaggerAppComponent is not generated
    kapt  "com.google.dagger:dagger-android-support:2.12"

    // rx-java and rx-android - reactive programming
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroidVer"
    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVer"

    // material progress bar with not min height
    implementation "me.zhanghai.android.materialprogressbar:library:1.4.1"

    // for drag dismiss
    implementation 'com.github.Commit451:ElasticDragDismissLayout:1.0.4'

    implementation 'com.github.chrisbanes:PhotoView:2.1.3'

    implementation "android.arch.persistence.room:runtime:1.0.0"
    kapt "android.arch.persistence.room:compiler:1.0.0"

    implementation 'com.robinhood.spark:spark:1.2.0'

    // testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
